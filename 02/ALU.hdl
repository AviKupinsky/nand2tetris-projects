// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //zx, nx:
    Mux16(a=x, b[0..15]=false, sel=zx, out=tempforx);
    Not16(in=tempforx, out=temp1);
    Mux16(a=tempforx, b=temp1, sel=nx, out=outforx);
    
    //zy, ny:
    Mux16(a=y, b[0..15]=false, sel=zy, out=tempfory);
    Not16(in=tempfory, out=temp2);
    Mux16(a=tempfory, b=temp2, sel=ny, out=outfory);

    //f:
    Add16(a=outforx, b=outfory, out=newtemp1);
    And16(a=outforx, b=outfory, out=newtemp2);
    Mux16(a=newtemp2, b=newtemp1, sel=f, out=xAndy);
 
    //no, ng, zr:
    Not16(in=xAndy, out=new);
    Mux16(a=xAndy, b=new, sel=no, out[0..7]=first, out[8..15]=second, out[15]=ng, out=out);
    Or8Way(in=first, out=out1);
    Or8Way(in=second, out=out2);
    Or(a=out1, b=out2, out=lastout);
    Not(in=lastout, out=zr);
}